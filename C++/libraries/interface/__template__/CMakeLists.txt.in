#-----------------------------------------------------------------------------------------------------------------------
# Information
#-----------------------------------------------------------------------------------------------------------------------
cmake_minimum_required(VERSION @CMAKE_VERSION@)
project(@NAME@
    VERSION @VERSION@
    DESCRIPTION @DESCRIPTION@
    LANGUAGES CXX)

cmake_file_api(
        QUERY
        API_VERSION 1
        CODEMODEL 2.3
)


#-----------------------------------------------------------------------------------------------------------------------
# Includes
#-----------------------------------------------------------------------------------------------------------------------
include(GNUInstallDirs)
include(CMakePackageConfigHelpers)


#-----------------------------------------------------------------------------------------------------------------------
# Settings and Options
#-----------------------------------------------------------------------------------------------------------------------
string(COMPARE EQUAL "${CMAKE_SOURCE_DIR}" "${CMAKE_CURRENT_SOURCE_DIR}" IS_TOP_LEVEL)

option(@NAME_UPPER@_BUILD_DOCS "Build @NAME@ documentation" OFF)

set(@NAME_UPPER@_INSTALL_CMAKEDIR "${CMAKE_INSTALL_LIBDIR}/cmake/@NAME@" CACHE STRING
    "Install path for @NAME@ package-related CMake files")

if(NOT DEFINED CMAKE_BUILD_TYPE AND NOT DEFINED CMAKE_CONFIGURATION_TYPES)
    set(CMAKE_BUILD_TYPE Release CACHE STRING "Build type" FORCE)
    set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS "Debug" "Release" "MinSizeRel" "RelWithDebInfo")
endif()

add_library(@NAME@ INTERFACE)
add_library(@NAMESPACE@::@NAME@ ALIAS @NAME@)


#-----------------------------------------------------------------------------------------------------------------------
# Sources
#-----------------------------------------------------------------------------------------------------------------------
add_subdirectory(tests)
add_subdirectory(src)

set(SOURCES
    include/@NAME@/@NAME@.hpp)
source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}" FILES ${SOURCES})


#-----------------------------------------------------------------------------------------------------------------------
# Targets
#-----------------------------------------------------------------------------------------------------------------------
target_sources(@NAME@ INTERFACE
    "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/lambda/lambda.hpp>")

target_compile_definitions(@NAME@ INTERFACE "${@NAME_UPPER@_INTERFACE_DEFINE}")

target_include_directories(@NAME@
    INTERFACE
        "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>"
        "$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>"
        "$<INSTALL_INTERFACE:include>")


#-----------------------------------------------------------------------------------------------------------------------
# Dependencies
#-----------------------------------------------------------------------------------------------------------------------


#-----------------------------------------------------------------------------------------------------------------------
# Installation
#-----------------------------------------------------------------------------------------------------------------------
if(NOT CMAKE_SKIP_INSTALL_RULES)
    configure_package_config_file(cmake/@NAME@-config.cmake.in @NAME@-config.cmake
        INSTALL_DESTINATION "${@NAME_UPPER@_INSTALL_CMAKEDIR}")

    write_basic_package_version_file(@NAME@-config-version.cmake
        COMPATIBILITY SameMajorVersion)

    install(DIRECTORY include/
        TYPE INCLUDE
        COMPONENT @NAME@-dev)

    set(TARGETS_FILE "@NAME@-interface-targets.cmake")

    install(TARGETS @NAME@
        EXPORT @NAME@-interface-targets
        COMPONENT @NAME@-dev)

    install(EXPORT @NAME@-interface-targets
        NAMESPACE @NAMESPACE@::
        COMPONENT @NAME@-dev
        DESTINATION "${@NAME_UPPER@_INSTALL_CMAKEDIR}")

    install(FILES
        "${CMAKE_CURRENT_BINARY_DIR}/@NAME@-config.cmake"
        "${CMAKE_CURRENT_BINARY_DIR}/@NAME@-config-version.cmake"
        COMPONENT @NAME@-dev
        DESTINATION "${@NAME_UPPER@_INSTALL_CMAKEDIR}")

endif()

if(@NAME_UPPER@_BUILD_DOCS)
    find_package(Doxygen REQUIRED)
    doxygen_add_docs(docs include)
endif()


#-----------------------------------------------------------------------------------------------------------------------
# Uninstallation
#-----------------------------------------------------------------------------------------------------------------------
if(NOT TARGET uninstall)
  configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/cmake/cmake_uninstall.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
    IMMEDIATE @ONLY)

  add_custom_target(uninstall COMMAND ${CMAKE_COMMAND} -P "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake")
endif()
